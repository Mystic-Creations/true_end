import org.gradle.api.logging.LogLevel

plugins {
    id 'eclipse'
    id 'net.minecraftforge.gradle' version '[6.0.16,6.2)'
    id 'org.spongepowered.mixin' version '0.7.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

repositories {
    maven { url = "https://maven.minecraftforge.net/" }
    maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
    maven { url = "https://maven.shedaniel.me/" }
    maven { url = "https://maven.theillusivec4.top/" }
    maven { url = "https://cursemaven.com/" }
    maven { url = "https://jitpack.io/" }
    maven { url = "https://maven.bawnorton.com/releases" }
    maven { url = "https://maven.enjarai.dev/mirrors" }

    flatDir { dirs "libs" }
}

version = "${project.modVersion}_${project.loaderType}-${project.mcVer}"
group = "net.mysticcreations.true_end"
archivesBaseName = "TrueEnd"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'parchment', version: '2023.09.03-1.20.1'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    copyIdeResources = true

    runs {
        client {
        }

        server {
        }

        configureEach {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
            }
        }
    }
}

dependencies {
    //Dependencies
    minecraft 'net.minecraftforge:forge:1.20.1-47.4.8'
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    implementation fg.deobf('com.github.glitchfiend:TerraBlender-forge:1.20.1-3.0.1.10')
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.5.0"))
    implementation(jarJar("io.github.llamalad7:mixinextras-forge:0.5.0")) {
        jarJar.ranged(it, "[0.5.0,)")
    }
    compileOnly(annotationProcessor("com.github.bawnorton.mixinsquared:mixinsquared-common:0.3.6-beta.1"))
    implementation(jarJar("com.github.bawnorton.mixinsquared:mixinsquared-forge:0.3.6-beta.1")) {
        jarJar.ranged(it, "[0.3.6-beta.1,)")
    }

    //Performance
    implementation fg.deobf("curse.maven:embeddium-908741:5681725")

    //APIs
    implementation fg.deobf("curse.maven:architectury-api-419699:5137938")
    implementation fg.deobf("me.shedaniel.cloth:cloth-config-forge:11.1.136")

    //Compatibility updates
    //Keep this in here
    implementation fg.deobf("top.theillusivec4.curios:curios-forge:5.4.3+1.20.1")
    implementation fg.deobf("curse.maven:accessories-938917:6201361")
    implementation fg.deobf("curse.maven:nostalgic-tweaks-592465:6800425")
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

mixin {
    add sourceSets.main, "refmap.true_end.json"
    config 'mixins.true_end.json'
}