plugins {
    id 'eclipse'
    id 'net.minecraftforge.gradle' version '[6.0.16,6.2)'
    id 'org.spongepowered.mixin' version '0.7.+'
}

repositories {
    maven { url = 'https://maven.minecraftforge.net/' }
    maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
    maven { url = "https://maven.shedaniel.me/" }
    maven { url = "https://maven.theillusivec4.top/" }
    maven { url = "https://cursemaven.com/" }
    maven { url 'https://jitpack.io/' }
}

//x.x.x.x_Forge/Fabric-x.xx.x_TestBuildnn
version = '1.4.2.1_Forge-1.20.1'
group = 'net.justmili.trueend'
archivesBaseName = 'TrueEnd'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'official', version: '1.20.1'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    copyIdeResources = true

    runs {
        client {
        }

        server {
        }

        configureEach {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.20.1-47.4.6'
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    implementation fg.deobf('com.github.glitchfiend:TerraBlender-forge:1.20.1-3.0.1.10')
    implementation fg.deobf("me.shedaniel.cloth:cloth-config-forge:11.1.136")
    implementation fg.deobf("top.theillusivec4.curios:curios-forge:5.4.3+1.20.1")
    implementation fg.deobf("curse.maven:artifacts-312353:4631664")
    implementation fg.deobf("curse.maven:architectury-api-419699:5137938")
    implementation fg.deobf("curse.maven:nostalgic-tweaks-592465:6800425")
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.5.0"))
    implementation(jarJar("io.github.llamalad7:mixinextras-forge:0.5.0")) {
        jarJar.ranged(it, "[0.5.0,)")
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
tasks.named('jar', Jar) {
    destinationDirectory.set(layout.projectDirectory.dir('.builtJars'))
    archiveFileName.set("${archivesBaseName}-${version}.jar")
}